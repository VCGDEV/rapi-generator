plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group 'com.vcgdev.plugin'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'rapi-generator'
            version = '1.0'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'REST API generator'
                description = 'Generate endpoints based on entity description'
                developers {
                    developer {
                        id = 'vcgdev'
                        name = 'Victor de la Cruz'
                        email = 'vcg_cruz@hotmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
}
